buildscript {
    ext {
        springBootVersion = '2.1.0.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "com.commercehub.cucumber-jvm" version "0.14"
    id 'com.palantir.docker' version '0.20.1'
    id 'com.palantir.docker-run' version '0.20.1'
    id 'com.avast.gradle.docker-compose' version "0.8.12"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

wrapper {
    gradleVersion = '4.9'
}

version = "0.1-SNAPSHOT"
group = "com.pillar"
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

Integer integrationTestPort = 8080
String integrationTestBaseUrl = "http://localhost:${integrationTestPort}"
String integrationTestMysql = "jdbc:mysql://localhost:3306/<DB>"

addCucumberSuite 'cucumberTest'
cucumberTest {
    stepDefinitionRoots = ['com.pillar']
    systemProperties = [
            "integration-endpoint": integrationTestBaseUrl,
            "integration-mysql": integrationTestMysql
    ]
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntimeOnly.extendsFrom testRuntimeOnly
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-webflux'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'mysql:mysql-connector-java'
    compile 'org.flywaydb:flyway-core'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
    cucumberTestCompile 'io.cucumber:cucumber-java:4.2.0'
    cucumberTestCompile 'org.springframework.boot:spring-boot-starter-logging'
    cucumberTestCompile 'io.cucumber:cucumber-junit:4.2.0'
}

docker {
    name "${project.group}/${bootJar.baseName}"
    files bootJar.outputs.files
    buildArgs([JAR_FILE: bootJar.archiveName])
    dockerfile project.file('src/main/resources/Dockerfile')
}

dockerCompose {
    mysqlOnly {
        startedServices = ['mysql']
    }
}

tasks.dockerRun.dependsOn('docker')
tasks.composeUp.dependsOn('docker')

task cucumberTestSetup(dependsOn: 'composeUp') {
    doLast {
        for (int i = 0; i < 30; i++) {
            try {
                new URL("${integrationTestBaseUrl}/actuator/health").text
                return
            } catch (IOException e) {
                sleep(1000)
            }
        }

        throw new GradleException("Unable to query api endpoint")
    }
}

task cucumberTestTeardown(dependsOn: 'composeDown') {}

cucumberTest.dependsOn('cucumberTestSetup')
cucumberTest.finalizedBy('cucumberTestTeardown')

task integrationTestSetup(dependsOn: 'mysqlOnlyComposeUp') {
    doLast {
        // Check on db server state
    }
}

task integrationTestTeardown(dependsOn: 'mysqlOnlyComposeDown') {
    doLast {

    }
}

task integrationTest(type: Test, dependsOn: 'integrationTestSetup') {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
    finalizedBy integrationTestTeardown
}

//check.dependsOn integrationTest
